Процедура ОбработкаПроведения(Отказ, Режим) 
	
	СтруктураУчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если СтруктураУчетнаяПолитика.СпособУчетаЗапасов = Перечисления.СпособыСписанияЗапасов.FEFO Тогда
		ОтображатьСрокиГодности = Истина;
	Иначе
		ОтображатьСрокиГодности = Ложь;
	КонецЕсли;
	
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.УчетЗатрат.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровИУслугТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ПоступлениеТоваровИУслугТовары.Количество) КАК Количество,
	|	1 КАК СтатьяЗатрат,
	|	ПоступлениеТоваровИУслугТовары.СрокГодности КАК СрокГодности,
	|	СУММА(ПоступлениеТоваровИУслугТовары.Сумма) КАК Сумма,
	|	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ПОМЕСТИТЬ ВТ_ТоварыИУслуги
	|ИЗ
	|	Документ.ПоступлениеТоваровИУслуг.Товары КАК ПоступлениеТоваровИУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровИУслугТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровИУслугТовары.Номенклатура,
	|	ПоступлениеТоваровИУслугТовары.СрокГодности,
	|	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровИУслугУслуги.Услуга,
	|	СУММА(ПоступлениеТоваровИУслугУслуги.Количество),
	|	ПоступлениеТоваровИУслугУслуги.СтатьяЗатрат,
	|	1,
	|	СУММА(ПоступлениеТоваровИУслугУслуги.Сумма),
	|	ПоступлениеТоваровИУслугУслуги.Услуга.СчетБухгалтерскогоУчета
	|ИЗ
	|	Документ.ПоступлениеТоваровИУслуг.Услуги КАК ПоступлениеТоваровИУслугУслуги
	|ГДЕ
	|	ПоступлениеТоваровИУслугУслуги.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровИУслугУслуги.Услуга,
	|	ПоступлениеТоваровИУслугУслуги.СтатьяЗатрат,
	|	ПоступлениеТоваровИУслугУслуги.Услуга.СчетБухгалтерскогоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТоварыИУслуги.Номенклатура КАК Номенклатура,
	|	СУММА(ВТ_ТоварыИУслуги.Количество) КАК Количество,
	|	ВТ_ТоварыИУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ВТ_ТоварыИУслуги.СрокГодности КАК СрокГодности,
	|	СУММА(ВТ_ТоварыИУслуги.Сумма) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ВТ_ТоварыИУслуги.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоТовар,
	|	ВТ_ТоварыИУслуги.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ИЗ
	|	ВТ_ТоварыИУслуги КАК ВТ_ТоварыИУслуги
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТоварыИУслуги.СрокГодности,
	|	ВЫБОР
	|		КОГДА ВТ_ТоварыИУслуги.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ВТ_ТоварыИУслуги.СтатьяЗатрат,
	|	ВТ_ТоварыИУслуги.Номенклатура,
	|	ВТ_ТоварыИУслуги.СчетБухгалтерскогоУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	СрокГодности";
	
	Выборка = запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоТовар Тогда		
			Движение = Движения.ТоварыНаСкладах.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Количество = выборка.Количество;
			Движение.Склад = Склад;
			Движение.Сумма = Выборка.Сумма;
			Если ОтображатьСрокиГодности Тогда
				Движение.СрокГодности = Выборка.СрокГодности;
			КонецЕсли;
			
		Иначе
			Движение = Движения.УчетЗатрат.Добавить();
			Движение.Период = Дата;
			Движение.СтатьяЗатрат = Выборка.СтатьяЗатрат;
			Движение.Сумма = Выборка.Сумма;
		КонецЕсли;
		
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = Выборка.СчетБухгалтерскогоУчета;
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
		Движение.Период = Дата;
		Движение.Сумма = Выборка.Сумма;
		Если Выборка.ЭтоТовар Тогда
			Движение.Содержание = "Отражено поступление товарно-материальных ценостей от поставщика";
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = Выборка.Номенклатура;
		Иначе
			Движение.Содержание = "Отражено поступление услуг от поставщика";
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат] = Выборка.Номенклатура;
		КонецЕсли;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = Контрагент;
	КонецЦикла;
	
КонецПроцедуры    

	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
